" vim:set fileencoding=utf-8:
" Vim loads settings in this order (condensed from `:help startup`):
" 1. system configs
" 2. $VIMINIT (as an Ex command line) or ~/.vim/vimrc
" 3. (only if 'exrc' is set) $PWD/.vimrc or $PWD/.exrc
" 4. plugin/**/*.vim in 'runtimepath' (which includes ~/.vim)
" 5. load packages from 'packpath'
" 6. after/plugin/**/*.vim in 'runtimepath' (which includes ~/.vim)
" 7. -c/+cmd command line options

" "Tiny-mode" vim is very restricted compared to standard vim.
" It doesn't even really support 'if' statements.
" Accordingly, this file sets as many prefs as possible that will work with
" basic vim, then re-sets them when we know we are not using tiny-mode vim.

" Get my simple prefs from .exrc.
source ~/.exrc
" But undo its mappings so they don't interfere with the ones I set below
unmap 
unmap! 

" GENERAL SETTINGS {{{1

silent! language en_US.UTF-8        " fails on Windows
set encoding=utf-8
set fileencodings=ucs-bom,utf-8,latin1
set modeline                        " allow modelines to work
set backspace=indent,eol,start      " backspace across lines, etc.
set expandtab                       " expand tabs to spaces
set softtabstop=-1                  " soft tab stops match shiftwidth
set shiftround                      " force <</>> to always align to shiftwidth
set noincsearch                     " disable incremental search
set hlsearch                        " hilight search terms
set showcmd                         " show current pending command in last line
set splitbelow splitright           " new splits start beneath/right of current

set scrolloff=4                     " number of lines to keep visible at top/bottom of screen

set updatetime=250                  " save swap and update CursorHold
set autoread                        " automatically reload the file if it's changed outside of vim and not inside

set display=lastline                " don't show partial lines as @'s
set lazyredraw                      " don't redraw screen during macro execution

set cinoptions+=t0                  " don't indent C function types
set cinoptions+=:0                  " C case labels at same indentation as switch
set cinoptions+=l1                  " C case labels align { } around case as I want
set cinoptions+=N-s                 " C++ namespace doesn't affect indent
set cinoptions+=E-s                 " C++ 'extern C' doesn't affect indent
set cinoptions+=(0                  " really, just read the help at this point
set cinoptions+=ks                  " extra indent in eg. if() conditions across multiple lines
set cinoptions+=j1                  " indent Java anonymous classes
set cinoptions+=J1                  " indent Javascript object declarations
" see also 'cinkeys' which can cause reindenting in Insert mode

silent! set signcolumn=yes          " always show sign column

" My default seems to be croql; 'help fo-table' says the default is tcq
set formatoptions=                  " clear it out
set formatoptions+=t                " auto-wrap text on textwidth
set formatoptions+=c                " auto-wrap comments on textwidth
set formatoptions+=r                " auto-insert comment leader for Enter
set formatoptions+=o                " auto-insert comment leader for o/O
set formatoptions+=j                " remove comment leader when joining
set formatoptions+=q                " let 'gq' format comments
silent! set formatoptions+=/        " don't continue // comments

" Highlight the current line and column, and column after textwidth
" The colours for these are set below
set nocursorline nocursorcolumn colorcolumn=+1

" Use histogram-based diffs where possible
if has('nvim-0.3.2') || has("patch-8.1.0360")
    set diffopt=filler,internal,algorithm:histogram,indent-heuristic
endif

" Set the directory for saving swap files and undo history
set directory=$TEMP/daperry/vim//
set   undodir=$TEMP/daperry/vim//
set undofile
silent! call mkdir(&directory, "p", 0700)

set list                " Show non-printable pseudocharacters
" Fallback symbols if vim has trouble with UTF-8
set listchars=tab:>-,trail:-,extends:>,precedes:<
" The real symbols I want
silent! set listchars=tab:␉·,trail:·,extends:⏵,precedes:⏴,nbsp:␣
silent! set fillchars=vert:│,fold:‧,foldsep:┆,diff:\ ,foldopen:┬,foldclose:═

set linebreak           " When wrapping lines, break them at 'breakat' instead of anywhere
set showbreak=▷         " Show an indicator on soft-wrapped lines
set breakindent         " indent wrapped lines to the same level as the base line

" Command-line tab-completion enhancements
set wildmenu                " Show an auto-completion menu ←/→ changes choice (but see keybindings)
set wildmode=longest:full   " When tab is pressed, fill longest, then show menu

" Matched pairs
set matchpairs+=‘:’,“:”,«:»

set foldcolumn=2                    " Show a 2-char wide fold column at the left
set foldlevelstart=0                " And close all folds upon loading a file
set foldmethod=marker               " Default folding method

set hidden                          " lets you switch away from unsaved buffers

" Minimal statusline
set ruler                           " Only really affects what Ctrl-G shows
set laststatus=2                    " Always show status line
set statusline=%t%m%r%=%y%l:%c\ %P/%L

" auto-complete options; see :help ins-completion
" see also https://vimways.org/2018/you-should-be-using-tags-in-vim/
" see also https://vim.wikia.com/wiki/Make_Vim_completion_popup_menu_work_just_like_in_an_IDE
set complete=.,t,i
set completeopt=menuone,preview
set shortmess+=c

set foldopen+=jump,quickfix

" }}}1

" KEY MAPPINGS {{{1
" NB. I'm using <Space> instead of <Leader> here because vim-tiny
" doesn't allow reassigning mapleader

nnoremap <Space>/ :noh<CR>
nnoremap <Space>c /^\(<<<<<<<\\|=======\\|>>>>>>>\)<CR>

" Set current working directory to the directory holding the current file
nnoremap <Space>d :cd %:p:h<CR>:pwd<CR>
" Show current working directory
nnoremap <Space>p :pwd<CR>

set pastetoggle=<F9>                " key that toggles paste mode

" Since most of us have Ctrl-S hardwired into our brains
nnoremap <C-S> :wa<CR>
inoremap <C-S> <C-O>:wa<CR>

" Set Ctrl-Backspace to delete word (gui)
inoremap <C-BS> <C-W>
cnoremap <C-BS> <C-W>

" Set Ctrl-Backspace to delete word (tui)
inoremap <C-H> <C-W>
cnoremap <C-H> <C-W>

" Allow Ctrl-W to be something else
inoremap <C-W> <Nop>
cnoremap <C-W> <Nop>

" Use Enter and Backspace to go into/back from a tag definition
nmap <Enter> <C-]>
nmap g<Enter> g<C-]>
nmap <BS> <C-T>

" Make n/N searching always search forward/backward respectively
noremap <expr>n 'Nn'[v:searchforward].'zo'
noremap <expr>N 'nN'[v:searchforward].'zo'

" Use Shift-Insert to paste the clipboard at the current location
inoremap <S-Insert> <C-O>:set paste<CR><C-R>*<C-O>:set nopaste<CR>
cnoremap <S-Insert> <C-R>*

" Don't let 'x' wipe out the delete register
noremap x "_x

" Move through visual lines rather than physical lines
noremap j gj
noremap k gk
noremap 0 g0
noremap $ g$

" Vi treats 'Y' like 'yy' ;
" this is more consistent with 'C'/'D'
noremap Y y$

" Some nice tweaks to working with visual mode
" `gv` re-selects the last selection
vmap < <gv
vmap > >gv

" pairing with gv, reselect pasted text
nnoremap gp `[v`]

" (open and) focus the quickfix window
nnoremap <Space>q :copen<CR>

" (open and) focus the location window
nnoremap <Space>l :lopen<CR>

" :help changelist
nnoremap ]c g,
nnoremap [c g;

" Use ↑/↓ in the wildmenu (see above)
" Source: https://vi.stackexchange.com/a/22628/9661
set wildcharm=<C-Z>
cnoremap <expr> <up> wildmenumode() ? "\<left>" : "\<up>"
cnoremap <expr> <down> wildmenumode() ? "\<right>" : "\<down>"
cnoremap <expr> <left> wildmenumode() ? "\<up>" : "\<left>"
cnoremap <expr> <right> wildmenumode() ? " \<bs>\<C-Z>" : "\<right>"

" List buffers and let you choose one
nnoremap <Space>b :ls<cr>:b<space>

" autocomplete from tag
inoremap <C-]> <C-X><C-]>
" autocomplete from keywords in the current and included files
inoremap <C-N> <C-X><C-I>
" autocomplete from definitions/macros
inoremap <C-D> <C-X><C-D>

" Neovide sees C-6 as different from C-^
nnoremap <C-6> <C-^>

" For parity with 'D' in visual mode
vnoremap Y :'<,'>y<cr>
" }}}1

" *********************
" Anything after this line is not seen by vim-tiny.
if !1 | finish | endif
" *********************

if exists('&renderoptions')
    set renderoptions=type:directx,gamma:1.0,contrast:0.5,level:1,geom:1,renmode:4,taamode:1
endif

let mapleader=" "

" PLUGINS {{{1

" Much of this configuration must be done before plugins are loaded,
" so they affect the plugins at load time.
" Configuration {{{2
let g:tagbar_type_diff = {
    \'ctagstype':'diff',
    \'kinds': [ 'f:filename' ],
    \'sort':0
\}
let g:tagbar_type_make = {
    \'kinds':[
        \ 'm:macros',
        \ 't:targets'
    \]
\}

let g:xml_syntax_folding=1      " see :help xml-folding
let g:c_no_comment_fold=1

let g:UltiSnipsJumpForwardTrigger = '<tab>'
let g:UltiSnipsJumpBackwardTrigger = '<s-tab>'

let g:syntastic_check_on_open = 1
let g:syntastic_check_on_wq = 0
let g:syntastic_aggregate_errors = 1
let g:syntastic_enable_signs = 1
let g:syntastic_always_populate_loc_list = 1
let g:syntastic_auto_loc_list = 0
let g:syntastic_enable_perl_checker = 1
let g:syntastic_c_checkers = ['clang_tidy', 'gcc']
let g:syntastic_python_checkers = ['python3', 'pycodestyle']
let g:syntastic_perl_checkers = ['perl', 'podchecker']
let g:syntastic_xml_checkers = ['xmllint']
let g:syntastic_sh_checkers = ['sh']
let g:syntastic_lua_checkers = ["luac"]
let g:syntastic_error_symbol = "\u2717"
let g:syntastic_warning_symbol = "\u26A0"
let g:syntastic_c_config_file = "c.syntastic"
let g:syntastic_json_checkers = ["jsonlint"]

let g:clever_f_across_no_line = 1       " disable multi-line search
let g:clever_f_fix_key_direction = 1    " f/t always forwards, F/T always back

let g:TerminusCursorShape = 0 " Don't change cursor shape (plays funny w/tmux)

let g:SimpylFold_fold_import = 0
let g:SimpylFold_fold_docstring = 0

let skeletons#autoRegister = 1

let g:EditorConfig_max_line_indicator = 'line'

let g:rustfmt_autosave = 1

let g:bufExplorerDisableDefaultKeyMapping = 1
let g:bufExplorerShowUnlisted = 1
let g:bufExplorerSortBy = 'fullpath'

let g:netrw_sort_sequence='[\/]$,*'
set suffixes-=.h            " vim-vinegar uses this setting

let g:ale_sign_column_always = 1
let g:ale_sign_error = "\u2717"
let g:ale_sign_warning = "\u26A0"
let g:ale_completion_enabled = 1
let g:ale_hover_cursor = 0
let g:ale_hover_to_preview = 1
let g:ale_lint_on_insert_leave = 1
let g:ale_lint_on_text_changed = "normal"
let g:ale_completion_delay = 500
let g:ale_lint_delay = 500
let g:ale_ccls_init_options = {
            \ 'cache': {
            \     'directory': '/home/$USER/.cache/ccls',
            \     'format': 'binary'
            \     }
            \ }
let g:ale_python_auto_pipenv = 1

nnoremap <F8> :TagbarToggle<CR>

nmap ]h <Plug>(GitGutterNextHunk)
nmap [h <Plug>(GitGutterPrevHunk)

" for wellle/targets.vim
" "change in argument" but don't eat all the whitespace
nmap cia cIa

" for inkarkat/vim-mark
let g:mwDefaultHighlightingPalette = 'extended'

nmap <Leader>m <Plug>MarkSet
nmap <Leader>r <Plug>MarkRegex
nmap <Leader>M <Plug>MarkConfirmAllClear
nmap ]m <Plug>MarkSearchAnyNext
nmap [m <Plug>MarkSearchAnyPrev
nmap <Plug>IgnoreMarkSearchNext <Plug>MarkSearchNext
nmap <Plug>IgnoreMarkSearchPrev <Plug>MarkSearchPrev

" for vim-pencil
let g:pencil#conceallevel = 0

" For vim-airline
let g:airline_powerline_fonts = 1
let g:airline_detect_paste = 1
"let g:airline_inactive_alt_sep=1
let g:airline_skip_empty_sections = 1

" }}}2

" Auto-installation {{{2
let $VIMHOME=expand('<sfile>:p:h')
" vim-plug (https://github.com/junegunn/vim-plug) settings
" Automatically install vim-plug and run PlugInstall if vim-plug not found
if empty(glob('$VIMHOME/autoload/plug.vim')) && executable('curl')
  silent !curl -fLo ~/.vim/autoload/plug.vim --create-dirs
    \ https://raw.githubusercontent.com/junegunn/vim-plug/master/plug.vim
  autocmd VimEnter * PlugInstall | source $MYVIMRC
endif
" }}}2

silent! call plug#begin('$VIMHOME/bundle') " {{{2
" use silent! to suppress warning messages about missing git in Windows
Plug 'junegunn/vim-plug'        " The plugin manager itself
Plug 'tpope/vim-vinegar'        " Improvements to netrw, which press '-' to see
Plug 'tpope/vim-characterize'   " type `ga` to show the unicode codepoint
Plug 'tpope/vim-fugitive'       " Git wrapper
Plug 'tpope/vim-repeat'         " Repeat supported plugin actions with `.`
Plug 'tpope/vim-unimpaired'     " Bracket mappings such as `[a` `]a` for args…
Plug 'tpope/vim-surround'       " cs([ - change () to []
Plug 'tpope/vim-commentary'     " Comment stuff out with `gc…`
Plug 'tpope/vim-apathy'         " make `gf` etc. less C-specific
"Plug 'tpope/vim-endwise'        " auto-add `endif` etc. when appropriate
Plug 'justinmk/vim-sneak'       " Use `sxy` to jump to next occurrence of xy
Plug 'tpope/vim-eunuch'         " UNIX shell commands, make '#!' files +x
Plug 'wellle/targets.vim'       " Text objects, eg. `di'` delete inside quote
Plug 'chaoren/vim-wordmotion'   " Let `w`/`e` motion recognize underscores, camelcase, etc.
Plug 'preservim/tagbar'         " tags sidebar, lets me show tag in statusline
Plug 'wincent/terminus'         " improve terminal vim (incl. bracketed paste)
Plug 'airblade/vim-gitgutter'   " git changes in gutter, also `c` text object
Plug 'inkarkat/vim-ingo-library' " Prereq for next plugin
Plug 'inkarkat/vim-mark'        " Mark words with <leader>m
"Plug 'morhetz/gruvbox'          " Fancy theme
"Plug 'Lokaltog/vim-monotone'    " Monotone colorscheme
Plug 'andreypopp/vim-colors-plain' " Very plain colorscheme
Plug 'junegunn/goyo.vim', { 'on': 'Goyo' } " Distraction-free, screen-centred writing
Plug 'preservim/vim-pencil'      " Improve vim for prose/text writing
Plug 'editorconfig/editorconfig-vim' " support editorconfig files
Plug 'wsdjeg/vim-fetch'         " Handle filename.cpp:132:5 to go to line 132
Plug 'Vimjas/vim-python-pep8-indent' " Just what it sounds like
Plug 'rhysd/vim-clang-format'   " clang-format C/C++ code (:ClangFormat)
Plug 'https://gitlab.com/HiPhish/info.vim.git' " Read infodocs in vim
Plug 'Boolean263/sectional-couch.vim' " My section navigation plugin
Plug 'Boolean263/vim-filetype-debsymbols' " Debian symbols files
Plug 'godlygeek/tabular', { 'on': ['Tabularize', 'Tab'] } " Line up text based on a regexp
Plug 'jlanzarotta/bufexplorer'  " Improved buffer explorer
Plug 'rust-lang/rust.vim'       " Rust programming language support
Plug 'kana/vim-textobj-user'    " User-defined text objects, prereq for next plugin
Plug 'preservim/vim-textobj-sentence' " Enhance vim's idea of what a sentence is
Plug 'dense-analysis/ale'       " Asynchronous Linting Engine
Plug 'itchyny/lightline.vim'    " Light-weight status line plugin
Plug 'romainl/vim-qf'           " Handy mappings and functions for quickfix windows
"Plug 'vim-airline/vim-airline'      " For a fancier modeline
if has('python3')
    Plug 'SirVer/ultisnips'     " Code snippets (fails horribly if no python3)
    Plug 'pgilad/vim-skeletons' " file skeletons using ultisnips
    Plug 'psf/black', { 'tag': 'stable', 'for': 'python' } " Python code formatting
endif
if executable('i3')
    Plug 'mboughaba/i3config.vim'
endif
Plug 'Boolean263/vim-syntax-libconfig'
Plug 'vimwiki/vimwiki'          " a personal wiki for vim
Plug 'kana/vim-altr'            " Switch between eg. foo.c/foo.h
call plug#end() " }}}2
" Note that the plugins haven't actually been loaded yet. They've been added
" to 'runtimepath' so they will be loaded once this `.vimrc` file is finished.
" (See the comment at the very start of this file.)
" The distinction is important because it means we can't test/use plugin
" features in this file.

" }}}1

" nvim doesn't (yet) support clipboard=autoselect
" https://github.com/neovim/neovim/wiki/FAQ#copying-to-x11-primary-selection-with-the-mouse-doesnt-work
if has('nvim') && &clipboard !~ '\<autoselect'
    vnoremap <LeftRelease> "*ygv
endif

" for :grep
if executable('rg')
    set grepprg=rg\ --vimgrep
    set grepformat^=%f:%l:%c:%m
endif

